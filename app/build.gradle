apply plugin: 'com.android.application'

ext {
    SUPPORT_LIBRARY_VERSION = '27.0.2'
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.0'
    defaultConfig {
        applicationId "com.meiji.toutiao"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 13
        versionName "1.3.1"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
//            Properties props = new Properties()
//            props.load(new FileInputStream(file("/sign/sign.properties")))
            // 变量在 Travis CI 后台设置好
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file("../KeyStore.jks")
            storePassword System.getenv("STORE_PASSWORD")
        }
        debug {
            // 部分第三方SDK要绑定签名, 故使用 release 的签名, 方便测试
            // 使用 try catch 是防止 Travis CI 打包失败
            try {
                Properties props = new Properties()
                props.load(new FileInputStream(file("/sign/sign.properties")))
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PASSWORD']
                storeFile file(props['KEYSTORE_FILE'])
                storePassword props['STORE_PASSWORD']
            } catch (Exception e) {
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true  //设为false即可断点调试
            shrinkResources true  //自动移除不用资源
            debuggable false //设为true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为Toutiao_release_v0.1_2017-02-26.apk
                        def fileName = "Toutiao_release_v${defaultConfig.versionName}_${releaseTime()}.apk"
                        outputFileName = fileName
                    }
                }
            }
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation 'com.android.support:multidex:1.0.2'
    implementation "com.android.support:support-v13:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:support-annotations:${SUPPORT_LIBRARY_VERSION}"

    //    implementation "com.android.support:customtabs:$SUPPORT_LIBRARY_VERSION"

    //    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"

    // 解析JSON
    implementation 'com.google.code.gson:gson:2.8.2'

    // 图片加载
    implementation 'com.github.bumptech.glide:glide:3.7.0'

    // 解析HTML
    implementation 'org.jsoup:jsoup:1.10.2'

    // 图片浏览
    implementation 'com.github.chrisbanes.photoview:library:1.2.4'

    // 协议Dialog
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'

    // 节操播放器
    implementation 'fm.jiecao:jiecaovideoplayer:5.7'

    // retrofit 2
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // 持久化 Cookie
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // RxJava 2
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // RxLifecycle
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.2.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.1'

    // okhttp 3
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    debugImplementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    // MD dialog
    implementation 'com.afollestad.material-dialogs:commons:0.9.4.4'

    // 权限管理
    implementation('com.github.hotchemi:permissionsdispatcher:2.4.0') {
        exclude module: 'support-v13'
    }
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'

    // 调试工具
    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    testCompile 'junit:junit:4.12'

    // 简化版 Adapter
    implementation 'me.drakeet.multitype:multitype:3.4.0'

    // RxBinding
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'

    // 响应式排版布局
    implementation 'com.google.android:flexbox:0.2.7'

    // 滑动返回 https://oss.sonatype.org/content/repositories/snapshots/com/r0adkll/slidableactivity/
    implementation 'com.r0adkll:slidableactivity:2.0.7-SNAPSHOT'

    // 带刻度的 SeekBar
    implementation 'com.github.Jay-Goo:RangeSeekBar:v1.1.0'

    // 高亮功能提示效果 https://material.google.com/growth-communications/feature-discovery.html#feature-discovery-design
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.9.1'
}
