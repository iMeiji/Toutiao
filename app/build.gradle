apply plugin: 'com.android.application'

ext {
    SUPPORT_LIBRARY_VERSION = '25.4.0'
    BUILD_TOOLS = "26.0.0"
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.meiji.toutiao"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 13
        versionName "1.3.1"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
//            Properties props = new Properties()
//            props.load(new FileInputStream(file("/sign/sign.properties")))
            // 变量在 Travis CI 后台设置好
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file("../KeyStore.jks")
            storePassword System.getenv("STORE_PASSWORD")
        }
        debug {
            // 部分第三方SDK要绑定签名, 故使用 release 的签名, 方便测试
            // 使用 try catch 是防止 Travis CI 打包失败
            try {
                Properties props = new Properties()
                props.load(new FileInputStream(file("/sign/sign.properties")))
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PASSWORD']
                storeFile file(props['KEYSTORE_FILE'])
                storePassword props['STORE_PASSWORD']
            } catch (Exception e) {
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true  //设为false即可断点调试
            shrinkResources true  //自动移除不用资源
            debuggable false //设为true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为Toutiao_release_v0.1_2017-02-26.apk
                        def fileName = "Toutiao_release_v${defaultConfig.versionName}_${releaseTime()}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
//    compile "com.android.support:customtabs:$SUPPORT_LIBRARY_VERSION"
//    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
//    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    // 解析JSON
    compile 'com.google.code.gson:gson:2.8.0'
    // 图片加载
    compile 'com.github.bumptech.glide:glide:3.7.0'
    // 解析HTML
    compile 'org.jsoup:jsoup:1.10.1'
    // 图片浏览
    compile 'com.github.chrisbanes.photoview:library:1.2.4'
    // 协议Dialog
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.2'
    // 节操播放器
    compile 'fm.jiecao:jiecaovideoplayer:5.7'
    // retrofit 2
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    // 持久化 Cookie
    compile 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    // RxJava 2
    compile 'io.reactivex.rxjava2:rxjava:2.0.9'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // RxLifecycle
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    // okhttp 3
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    debugCompile 'com.squareup.okhttp3:logging-interceptor:3.7.0'
    // MD dialog
    compile 'com.afollestad.material-dialogs:commons:0.9.4.4'
    // 权限管理
    compile('com.github.hotchemi:permissionsdispatcher:2.4.0') { exclude module: 'support-v13' }
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.4.0'
    // 调试工具
    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    testCompile 'junit:junit:4.12'
    // 简化版 Adapter
    compile 'me.drakeet.multitype:multitype:3.0.0'
    // RxBinding
    compile 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'
    // 响应式排版布局
    compile 'com.google.android:flexbox:0.2.7'
    // 滑动返回 https://oss.sonatype.org/content/repositories/snapshots/com/r0adkll/slidableactivity/
    compile 'com.r0adkll:slidableactivity:2.0.7-SNAPSHOT'
    // 带刻度的 SeekBar
    compile 'com.github.Jay-Goo:RangeSeekBar:v1.0.7'
    // 高亮功能提示效果 https://material.google.com/growth-communications/feature-discovery.html#feature-discovery-design
    compile 'com.getkeepsafe.taptargetview:taptargetview:1.9.1'
}
